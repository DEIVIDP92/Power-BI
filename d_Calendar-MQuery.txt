-------------- CALENDAR TABLE IN POWER QUERY --------------

let
    //Identify today's date
    Today = DateTime.Date(DateTime.LocalNow()),
    //Returns the first of January of the corresponding year to the minimum date (subtracting 2 years to start in 2010)
    MinDate = #date(2019, 1, 1),
    //Returns the thirty-first of December of the corresponding year to the maximum date (adding 3 years to always have three years ahead of the last date)
    MaxDate = Date.AddYears(Date.EndOfYear(Today), 0),
    //Calculates the difference in days between the maximum date and the minimum date and adds 1 to always include the 31/12/LastYear
    Duration = Duration.Days(Duration.From(MaxDate - MinDate)) + 1,
    //Creates a list of dates from MinDate to MaxDate, increasing by one day
    Date = List.Dates(MinDate, Duration, #duration(1,0,0,0)),
    //Converts the list into a table (the base of the calendar)
    #"Converted to Table" = Table.FromList(Date, Splitter.SplitByNothing(), null, null, ExtraValues.Error),
    //Renames the columns of the table
    #"Renamed Columns" = Table.RenameColumns(#"Converted to Table",{{"Column1", "Date"}}),
    //Changes the data type of the Date column to date
    #"Changed Type" = Table.TransformColumnTypes(#"Renamed Columns",{{"Date", type date}}),
    //Adds a Year column
    Year = Table.AddColumn(#"Changed Type", "Year", each Date.Year([Date])),
    // Adds a Semester column
    SemesterColumn = Table.AddColumn(Year, "Semester", each "S" & Text.From(Number.RoundUp(Date.Month([Date]) / 6, 0))),
    // Adds a Semester column but in intenger format
    SemesterColumnNum = Table.AddColumn(SemesterColumn, "SemesterNum", each Text.From(Number.RoundUp(Date.Month([Date]) / 6, 0))),
    // Adds a Quarter column
    QuarterColumn = Table.AddColumn(SemesterColumnNum, "Quarter", each "Q" & Text.From(Number.RoundUp(Date.Month([Date]) / 3, 0))),
    //Adds a Quarte column in intenger forma
    QuarterColumnNum = Table.AddColumn(QuarterColumn, "QuarterNum", each Text.From(Number.RoundUp(Date.Month([Date]) / 3, 0))),
    //Adds a YearMonth column
    YearMonth = Table.AddColumn(QuarterColumnNum, "YearMonthNum", each Date.ToText([Date], "yyyyMM")),
    //Adds a Month Number column
    MonthNum = Table.AddColumn(YearMonth, "MonthNum", each Date.Month([Date])),
    //Adds a Month column
    MonthColumn = Table.AddColumn(MonthNum, "Month", each Date.MonthName([Date], "en-UK")),
    //Adds a Week Number column
    WeekNumber = Table.AddColumn(MonthColumn, "WeekNum", each Date.WeekOfYear([Date])),
    //Adds a Day Number column
    DayNumber = Table.AddColumn(WeekNumber, "DayNum", each Date.DayOfWeek([Date]) + 1),
    //Adds a Day column
    Day = Table.AddColumn(DayNumber, "Day", each Date.DayOfWeekName([Date])),
    //Changes all data type to avoid errors when duplicating columns
    #"Changed Type1" = Table.TransformColumnTypes(Day,{{"Year", type text}, {"Semester", type text}, {"SemesterNum", Int64.Type}, {"Quarter", type text}, {"QuarterNum", Int64.Type}, {"YearMonthNum", Int64.Type}, {"MonthNum", Int64.Type}, {"Month", type text}, {"WeekNum", Int64.Type}, {"DayNum", Int64.Type}, {"Day", type text}}),
    //Shorten version of Month
    MothShort = Table.AddColumn(#"Changed Type1", "MonthShort", each Text.Start([Month], 3)),
    //Shorten version of Day
    DayShort = Table.AddColumn(MothShort, "DayShort", each Text.Start([Day], 3)),
    //Shorten version of Year
    YearShort = Table.AddColumn(DayShort, "YearShort", each Text.End([Year], 2)),
    //Changes all data type to avoid errors when duplicating columns
    #"Changed Type3" = Table.TransformColumnTypes(YearShort,{{"MonthShort", type text}, {"DayShort", type text}, {"YearShort", type text}}),
    //Shorten version of MonthYear
    MonthYearShort = Table.AddColumn(#"Changed Type3", "MonthYearShort", each Text.Combine({[MonthShort], [YearShort]}, " - "), type text),
    //YearNum in intenger column
    YearNum = Table.DuplicateColumn(MonthYearShort, "Year", "YearNum"),
    //Changes the data type to avoid errors when duplicating columns
    #"Changed Type2" = Table.TransformColumnTypes(YearNum,{{"YearNum", Int64.Type}}),
    // Current year identifier
    CurrentYear = Table.AddColumn(#"Changed Type2", "CurrentYear", each if [YearNum] = Date.Year(DateTime.LocalNow()) then 1 else 0, Int64.Type),
    // Year to Date identifier
    YearToDate = Table.AddColumn(CurrentYear, "YearToDate", each if Date.Month([Date]) >= 1 and Date.Month([Date]) <= Date.Month(DateTime.LocalNow()) then 1 else 0 ),
    // Quarter to Date identifier
    QuarterToDate = Table.AddColumn(YearToDate, "QuarterToDate", each if Date.QuarterOfYear([Date]) <= Date.QuarterOfYear(DateTime.LocalNow()) then 1 else 0 ),
    // Month to Date identifier
    MonthToDate = Table.AddColumn(QuarterToDate, "MonthToDate",each 
    if [Date] >= #date(Date.Year(DateTime.LocalNow()), 2, 1) and [Date] <= Date.From(DateTime.LocalNow()) then 1 else 0),
    // 12 Months rolling period from today identifier
    //Months12Rolling = Table.AddColumn(QuarterToDate, "12MonthsRolling", each if ([Date] >= (Date.From(DateTime.LocalNow()) - #duration(365, 0, 0, 0)) and [Date] <= Date.From(DateTime.LocalNow()))  then 1 else 0),
    Months12Rolling = Table.AddColumn(MonthToDate, "12MonthsRolling", each 
    if ([Date] >= (Date.From(DateTime.LocalNow()) - #duration(365, 0, 0, 0)) and 
        [Date] <= Date.From(DateTime.LocalNow()) ) then 
        1 //Rolling 12 Months
    else if ([Date] >= (Date.From(DateTime.LocalNow()) - #duration(730, 0, 0, 0)) and 
             [Date] < (Date.From(DateTime.LocalNow()) - #duration(365, 0, 0, 0))) then 
        2 //Last Year Comparison
    else 
        0),
    #"Duplicated column" = Table.DuplicateColumn(Months12Rolling, "Date", "DateKey"),
    //Changes the previous columns data type
    #"Changed Type4" = Table.TransformColumnTypes(#"Duplicated column",{{"YearToDate", Int64.Type}, {"QuarterToDate", Int64.Type}, {"12MonthsRolling", Int64.Type}, {"CurrentYear", Int64.Type}, {"DateKey", Int64.Type}, {"MonthToDate", Int64.Type}})
in
    #"Changed Type4"
